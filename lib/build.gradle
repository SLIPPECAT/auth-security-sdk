plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.github.mimyo'
version = '0.1.0-SNAPSHOT'
description = 'Authentication and Authorization SDK'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

dependencies {
    // 코어 의존성
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.0'
    implementation 'org.slf4j:slf4j-api:1.7.32'

    // 보안 관련 라이브러리
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.2'

    // 테스트 의존성
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.0.0'
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'security-sdk-spring-boot-starter'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Security SDK Spring Boot Starter'
                description = 'A Spring Boot starter for security and authentication'
                url = 'https://github.com/SLIPPECAT/auth-security-sdk'
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mimyo'
                        name = 'junyeong'
                        email = 'ryjun91@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/SLIPPECAT/auth-security-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/SLIPPECAT/auth-security-sdk.git'
                    url = 'https://github.com/SLIPPECAT/auth-security-sdk'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/SLIPPECAT/auth-security-sdk")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
